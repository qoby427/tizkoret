plugins {
    id 'com.android.application'
}

def getVersionCode() {
    def versionPropsFile = file("version.properties")
    if (!versionPropsFile.exists()) {
        versionPropsFile.createNewFile()
        versionPropsFile.text = "VERSION_CODE=5"
    }

    def props = new Properties()
    props.load(new FileInputStream(versionPropsFile))

    def code = props["VERSION_CODE"].toInteger()

    def isReleaseBuild = gradle.startParameter.taskNames.any {
        it.toLowerCase().contains("release")
    }

    if (isReleaseBuild) {
        code++
        props["VERSION_CODE"] = code.toString()
        props.store(new FileOutputStream(versionPropsFile), null)
    }

    return code
}

def generateVersionName(int versionCode) {
    def now = java.time.LocalDate.now()
    def day = String.format("%02d", now.dayOfMonth)
    def month = String.format("%02d", now.monthValue)

    return "1.${day}${month}.${versionCode}"
}

def computedVersionCode = getVersionCode()
def computedVersionName = generateVersionName(computedVersionCode)

android {
    namespace 'com.emhillstudio.tizcoret'
    compileSdk 36

    defaultConfig {
        applicationId "com.emhillstudio.tizcoret"
        minSdk 23
        targetSdk 36
        versionCode computedVersionCode
        versionName computedVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            ndk {
                debugSymbolLevel 'none'
            }

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    // Remove duplicates
    implementation libs.appcompat
    implementation libs.activity
    implementation libs.constraintlayout.v214

    implementation libs.geo
    implementation libs.work.runtime
    implementation libs.billing.v610
    implementation libs.zmanim
    implementation libs.gson
    implementation libs.material.v1130
    implementation libs.work.runtime.v290
    implementation libs.play.services.location.v2120

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}
